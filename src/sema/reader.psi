import PSC::Reflection::*;
import SailGate::Prelude::Domain_Kind;
import SailGate::Sema::Sema_Error;
import SailGate::Util::Result;
import SailGate::IR;

/// Methods for interpreting ParaSail AST nodes as IR values
interface SailGate::Sema::Reader<> is

    type Domain_Result is Result<IR::Domain, Sema_Error>;
    type Type_Result is Result<IR::Type, Sema_Error>;

    /// Reads domain parameter from type parameter annotation
    func Dom_Param(D : Decl) -> Domain_Result;

    /// Reads clock domain from object declaration
    func Dom_Obj(D : Decl {Kind(D) == #object}) -> Domain_Result;

    /// Checks if type is an RTL type
    func Type_Is_Rtl(D : Decl {Kind(D) == #type}) -> Boolean;

    /// Reads RTL type info from the result of a Sem_Info call.
    /// This function returns an error if the type is not an RTL type
    func Type_To_Rtl(D : Decl {Kind(D) == #type};
        Pos : Source_Position) -> Type_Result;

    /// Adds offset and direction to existing RTL type based on information
    /// passed in the align tree. This align tree will be an annotation node
    func Add_Alignment(var T : IR::Type; Align_Tree : optional Tree)
        -> optional Sema_Error;

    /// Attempts to read tree as a constant integer. Returns null on failure
    func Read_Const_Int(T : Tree) -> optional Univ_Integer;

end interface SailGate::Sema::Reader
